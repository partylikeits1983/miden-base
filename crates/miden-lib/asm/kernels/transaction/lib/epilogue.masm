use.$kernel::account
use.$kernel::account_delta
use.$kernel::asset_vault
use.$kernel::constants
use.$kernel::memory
use.$kernel::note
use.$kernel::tx

use.std::crypto::hashes::rpo

#Â ERRORS
# =================================================================================================

const.ERR_ACCOUNT_NONCE_DID_NOT_INCREASE_AFTER_STATE_CHANGE="account nonce did not increase after a state changing transaction"

const.ERR_EPILOGUE_TOTAL_NUMBER_OF_ASSETS_MUST_STAY_THE_SAME="total number of assets in the account and all involved notes must stay the same"

const.ERR_EPILOGUE_EXECUTED_TRANSACTION_IS_EMPTY="executed transaction neither changed the account state, nor consumed any notes"

const.ERR_AUTH_PROCEDURE_CALLED_FROM_WRONG_CONTEXT="auth procedure had been called from outside the epilogue"

# CONSTANTS
# =================================================================================================

# Event emitted to signal that the compute_fee procedure has obtained the current number of cycles.
const.EPILOGUE_AFTER_TX_FEE_COMPUTED=131097

# The number of cycles the epilogue is estimated to take after compute_fee has been executed.
# This is _estimated_ using the transaction measurements on ExecutedTransaction.
# This should be replaced with a way to _calculate_ the number of cycles that the epilogue will incur
# after compute_fee.
const.ESTIMATED_AFTER_COMPUTE_FEE_CYCLES=1200

# OUTPUT NOTES PROCEDURES
# =================================================================================================

#! Copies the entire output note data to the advice map. If no notes were created by a transaction,
#! nothing is copied to the advice map.
#!
#! The output note data includes the note id, metadata, recipient, assets commitment, number of assets and
#! a set of assets for each note.
#!
#! Inputs:
#!   Operand stack: [OUTPUT_NOTES_COMMITMENT]
#! Outputs:
#!   Operand stack: [OUTPUT_NOTES_COMMITMENT]
#!   Advice map: {
#!      OUTPUT_NOTES_COMMITMENT: [mem[output_note_ptr]...mem[output_notes_end_ptr]],
#!   }
#!
#! Where:
#! - OUTPUT_NOTES_COMMITMENT is the note commitment computed from note's id and metadata.
#! - output_note_ptr is the start boundary of the output notes section.
#! - output_notes_end_ptr is the end boundary of the output notes section.
#! - mem[i] is the memory value stored at some address i.
proc.copy_output_notes_to_advice_map
    # get the number of notes created by the transaction
    exec.memory::get_num_output_notes
    # => [num_notes, OUTPUT_NOTES_COMMITMENT]

    # if there are output notes, add them to the advice map
    dup eq.0
    if.true
        # drop num_notes
        drop
    else
        # compute the end boundary of the output notes section
        exec.memory::get_output_note_ptr movdn.4
        # => [OUTPUT_NOTES_COMMITMENT, output_notes_end_ptr]

        # compute the start boundary of the output notes section
        exec.memory::get_output_note_data_offset movdn.4
        # => [OUTPUT_NOTES_COMMITMENT, output_note_ptr, output_notes_end_ptr]

        # insert created data into the advice map
        adv.insert_mem
        # => [OUTPUT_NOTES_COMMITMENT, output_note_ptr, output_notes_end_ptr]

        # drop output note pointers
        movup.4 drop movup.4 drop
    end
    # => [OUTPUT_NOTES_COMMITMENT]
end

# BUILD OUTPUT VAULT
# =================================================================================================

#! Builds the output vault which is combination of the assets in the account vault at the end of the
#! transaction and all the assets in the output notes.
#!
#! The output vault is built as follows:
#! - we first copy the account vault root to the output vault root.
#! - we then loop over the output notes and insert their assets into the output vault.
#!
#! Inputs:  []
#! Outputs: []
proc.build_output_vault
    # copy final account vault root to output account vault root
    exec.memory::get_acct_vault_root exec.memory::set_output_vault_root dropw
    # => []

    # get the number of output notes from memory
    exec.memory::get_num_output_notes
    # => [num_output_notes]

    # calculate the address at which we should stop looping
    exec.memory::get_output_note_ptr
    # => [output_notes_end_ptr]

    # compute pointer for the first output note
    push.0 exec.memory::get_output_note_ptr
    # => [output_note_ptr, output_notes_end_ptr]

    # check if the number of output notes is greater then 0. Conditional for the while loop.
    dup.1 dup.1 neq
    # => [should_loop, output_note_ptr, output_notes_end_ptr]

    # loop over output notes and add assets to output vault
    while.true
        # get the number of assets for the output note from memory
        dup exec.memory::get_output_note_num_assets
        # => [num_assets, note_data_ptr, output_notes_end_ptr]

        # prepare stack for reading output note assets
        exec.memory::get_output_vault_root_ptr dup.2 exec.memory::get_output_note_asset_data_ptr dup
        # => [assets_start_ptr, assets_start_ptr, output_vault_root_ptr, num_assets, note_data_ptr,
        #     output_notes_end_ptr]

        # compute the end pointer for output note asset looping
        dup.3 mul.4 add swap
        # => [assets_start_ptr, assets_end_ptr, output_vault_root_ptr, num_assets, note_data_ptr,
        #     output_notes_end_ptr]

        # assess if we should loop
        dup.1 dup.1 neq
        # => [should_loop, assets_start_ptr, assets_end_ptr, output_vault_root_ptr, num_assets,
        #     note_data_ptr, output_notes_end_ptr]

        # loop over output note assets and insert them into the output vault
        while.true
            # duplicate output_vault_root_ptr
            dup.2
            # => [output_vault_root_ptr, assets_start_ptr, assets_end_ptr, output_vault_root_ptr,
            #     num_assets, note_data_ptr, output_notes_end_ptr]

            # read the output note asset from memory
            padw dup.5 mem_loadw
            # => [ASSET, output_vault_root_ptr, assets_start_ptr, assets_end_ptr,
            #     output_vault_root_ptr, num_assets, note_data_ptr, output_notes_end_ptr]

            # insert output note asset into output vault
            exec.asset_vault::add_asset dropw
            # => [assets_start_ptr, assets_end_ptr, output_vault_root_ptr, num_assets,
            #     note_data_ptr, output_notes_end_ptr]

            # increment assets_start_ptr and asses if we should loop again
            add.4 dup.1 dup.1 neq
            # => [should_loop, assets_start_ptr, assets_end_ptr, output_vault_root_ptr, num_assets,
            #     note_data_ptr, output_notes_end_ptr]
        end

        # clean stack
        drop drop drop drop
        # => [note_data_ptr, output_note_end_ptr]

        # increment output note pointer and check if we should loop again
        exec.constants::get_note_mem_size add dup.1 dup.1 neq
        # => [should_loop, output_note_ptr, output_notes_end_ptr]
    end

    # clean stack
    drop drop
    # => []
end

# AUTH PROCEDURE
# =================================================================================================

#! Executes the auth procedure.
#!
#! Inputs:  []
#! Outputs: []
proc.execute_auth_procedure
    padw padw padw
    # get the auth procedure arguments
    exec.memory::get_auth_args
    # => [AUTH_ARGS, pad(12)]

    # auth procedure is at index 0 within the account procedures section.
    push.0 exec.memory::get_acct_procedure_ptr
    # => [auth_procedure_ptr, AUTH_ARGS, pad(12)]

    padw dup.4 mem_loadw
    # => [AUTH_PROC_ROOT, auth_procedure_ptr, AUTH_ARGS, pad(12)]

    # if auth procedure was called already, it must have been called by a user, which is disallowed
    exec.account::was_procedure_called
    # => [was_auth_called, auth_procedure_ptr, AUTH_ARGS, pad(12)]
    assertz.err=ERR_AUTH_PROCEDURE_CALLED_FROM_WRONG_CONTEXT

    # execute the auth procedure
    dyncall
    # => [OUTPUT_3, OUTPUT_2, OUTPUT_1, OUTPUT_0]

    # clean up auth procedure outputs
    dropw dropw dropw dropw
end

# FEE PROCEDURES
# =================================================================================================

#! Computes the fee required for the current transaction.
#!
#! The number of cycles a transaction will take to verify depends logarithmically on the number of
#! cycles that were proven. We estimate this by taking log2 of the estimated number of cycles the
#! transaction will take to execute, rounded up to the next power of two.
#!
#! Inputs:  []
#! Outputs: [fee_amount]
proc.compute_fee
    # get the number of cycles the transaction has taken to execute up this point
    clk
    # => [num_current_cycles]

    emit.EPILOGUE_AFTER_TX_FEE_COMPUTED

    # estimate the number of cycles the transaction will take
    add.ESTIMATED_AFTER_COMPUTE_FEE_CYCLES
    # => [num_tx_cycles]

    # ilog2 will round down, but we need to round up, so we add 1 afterwards.
    # technically we don't need to do this if num_tx_cycles is already a power of two, but
    # because we're estimating part of the cycle count anyway, this is not a correctness
    # concern currently.
    ilog2 add.1
    # => [num_estimated_verification_cycles]

    exec.memory::get_verification_base_fee
    # => [verification_base_fee, num_estimated_verification_cycles]

    mul
    # => [verification_cost]
end

# TRANSACTION EPILOGUE PROCEDURE
# =================================================================================================

#! Finalizes the transaction by performing the following steps:
#! - computes the account delta commitment and the final account commitment, and the merged
#!   hash of both.
#! - if the account has changed, assert that the final account nonce is greater than the initial
#!   account nonce.
#! - computes the output notes commitment.
#! - asserts that the input and output vault roots are equal.
#!
#! Inputs:  []
#! Outputs: [OUTPUT_NOTES_COMMITMENT, ACCOUNT_UPDATE_COMMITMENT, tx_expiration_block_num]
#!
#! Where:
#! - OUTPUT_NOTES_COMMITMENT is the commitment of the output notes.
#! - ACCOUNT_UPDATE_COMMITMENT is the hash of the the final account commitment and account
#!   delta commitment.
#! - tx_expiration_block_num is the transaction expiration block number.
export.finalize_transaction
    # make sure that the context was switched back to the native account
    exec.memory::assert_native_account

    # execute the account authentication procedure
    exec.execute_auth_procedure
    # => []

    # compute the fee the tx needs to pay
    # ignored for now
    exec.compute_fee drop
    # => []

    # get the initial account commitment
    exec.memory::get_init_account_commitment
    # => [INIT_ACCOUNT_COMMITMENT]

    # get the offset for the end of the account data section
    exec.memory::get_core_acct_data_end_ptr
    # => [acct_data_end_ptr, INIT_ACCOUNT_COMMITMENT]

    # get the offset for the start of the account data section
    exec.memory::get_current_account_data_ptr
    # => [acct_data_ptr, acct_data_end_ptr, INIT_ACCOUNT_COMMITMENT]

    # compute the final account commitment
    exec.account::compute_current_commitment
    # => [FINAL_ACCOUNT_COMMITMENT, acct_data_ptr, acct_data_end_ptr, INIT_ACCOUNT_COMMITMENT]

    # insert final account data into the advice map
    adv.insert_mem
    # => [FINAL_ACCOUNT_COMMITMENT, acct_data_ptr, acct_data_end_ptr, INIT_ACCOUNT_COMMITMENT]

    # drop account data section pointers
    movup.4 drop movup.4 drop
    # => [FINAL_ACCOUNT_COMMITMENT, INIT_ACCOUNT_COMMITMENT]

    # check if the account has changed
    eqw not
    # => [has_acct_changed, FINAL_ACCOUNT_COMMITMENT, INIT_ACCOUNT_COMMITMENT]

    # if the account has changed then apply nonce checks
    if.true
        # get initial nonce from memory
        exec.memory::get_init_nonce
        # => [init_nonce, FINAL_ACCOUNT_COMMITMENT, INIT_ACCOUNT_COMMITMENT]

        # get current nonce from memory
        exec.memory::get_acct_nonce
        # => [current_nonce, init_nonce, FINAL_ACCOUNT_COMMITMENT, INIT_ACCOUNT_COMMITMENT]

        # assert that initial nonce is less than current nonce
        lt assert.err=ERR_ACCOUNT_NONCE_DID_NOT_INCREASE_AFTER_STATE_CHANGE
        # => [FINAL_ACCOUNT_COMMITMENT, INIT_ACCOUNT_COMMITMENT]
    else
        # assert that this transaction had input notes, otherwise this transaction is empty, which 
        # is not allowed and will result in an error
        exec.memory::get_input_notes_commitment padw eqw
        # => [is_input_notes_commitment_empty, EMPTY_WORD, INPUT_NOTES_COMMITMENT, 
        #        FINAL_ACCOUNT_COMMITMENT, INIT_ACCOUNT_COMMITMENT]

        # assert that the input notes commitment is not an empty word, otherwise there were no input
        # notes in this transaction
        assertz.err=ERR_EPILOGUE_EXECUTED_TRANSACTION_IS_EMPTY dropw dropw
        # => [FINAL_ACCOUNT_COMMITMENT, INIT_ACCOUNT_COMMITMENT]
    end

    # drop initial account commitment
    swapw dropw
    # => [FINAL_ACCOUNT_COMMITMENT]

    exec.account_delta::compute_commitment
    # => [ACCOUNT_DELTA_COMMITMENT, FINAL_ACCOUNT_COMMITMENT]

    # insert into advice map ACCOUNT_UPDATE_COMMITMENT: (FINAL_ACCOUNT_COMMITMENT, ACCOUNT_DELTA_COMMITMENT),
    # where ACCOUNT_UPDATE_COMMITMENT = hash(FINAL_ACCOUNT_COMMITMENT || ACCOUNT_DELTA_COMMITMENT)
    adv.insert_hdword
    # => [ACCOUNT_DELTA_COMMITMENT, FINAL_ACCOUNT_COMMITMENT]

    hmerge
    # => [ACCOUNT_UPDATE_COMMITMENT]

    # build the output vault
    exec.build_output_vault
    # => [ACCOUNT_UPDATE_COMMITMENT]

    # compute output notes commitment
    exec.note::compute_output_notes_commitment
    # => [OUTPUT_NOTES_COMMITMENT, ACCOUNT_UPDATE_COMMITMENT]

    # copy output note data to the advice map
    exec.copy_output_notes_to_advice_map
    # => [OUTPUT_NOTES_COMMITMENT, ACCOUNT_UPDATE_COMMITMENT]

    # assert no net creation or destruction of assets over the transaction
    exec.memory::get_input_vault_root exec.memory::get_output_vault_root
    assert_eqw.err=ERR_EPILOGUE_TOTAL_NUMBER_OF_ASSETS_MUST_STAY_THE_SAME
    # => [OUTPUT_NOTES_COMMITMENT, ACCOUNT_UPDATE_COMMITMENT]

    exec.memory::get_expiration_block_num movdn.8
    # => [OUTPUT_NOTES_COMMITMENT, ACCOUNT_UPDATE_COMMITMENT, tx_expiration_block_num]
end
